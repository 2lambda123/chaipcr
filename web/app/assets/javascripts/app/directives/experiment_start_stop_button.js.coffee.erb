window.ChaiBioTech.ngApp
.directive 'experimentStartStopButton', [
  'Status'
  'Experiment'
  (Status, Experiment) ->

    restrict: 'EA'
    replace: true
    scope:
      experimentId: '='
    templateUrl: 'app/views/directives/experiment-start-stop-button.html'
    link: ($scope, elem) ->

      $scope.$watch 'experimentId', (val) ->
        if angular.isNumber val
          Experiment.get {id: $scope.experimentId}, (resp) ->
            $scope.experiment = resp.experiment
            $scope.init()

      $scope.init = ->
        Status.startSync()
        $scope.stopped = false

        $scope.$watch ->
          Status.getData()
        , (val) ->
          $scope.data = val

          if $scope.data?.experimentController?.machine.state is 'Idle'
            $scope.stopped = false

      elem.on '$destroy', ->
        Status.stopSync()

      $scope.startExperiment = (expId) ->
        Experiment.startExperiment(expId)

      $scope.stopExperiment = ->
        Experiment.stopExperiment().then ->
          $scope.stopped = true

      $scope.completedAndStopped = ->
        $scope.data?.experimentController?.machine.state is 'Complete' and $scope.stopped

]