window.ChaiBioTech.ngApp
.directive 'experimentStartStopButton', [
  'Status'
  'Experiment'
  (Status, Experiment) ->

    restrict: 'EA'
    replace: true
    scope:
      experimentId: '='
    templateUrl: 'app/views/directives/experiment-start-stop-button.html'
    link: ($scope, elem) ->
      Status.startSync()
      $scope.completedAndStopped = false

      $scope.$watch ->
        Status.getData()
      , (val) ->
        $scope.data = val

        if $scope.data?.experimentController?.machine.state is 'Idle'
          $scope.completedAndStopped = false

      elem.on '$destroy', ->
        Status.stopSync()

      $scope.startExperiment = (expId) ->
        Experiment.startExperiment(expId)

      $scope.stopExperiment = ->
        Experiment.stopExperiment().then ->
          $scope.completedAndStopped = true
]