window.ChaiBioTech.ngApp.directive 'timeScrollbar', [
  '$window'
  ($window) ->
    restrict: 'EA'
    replace: true
    templateUrl: 'app/views/directives/time-scrollbar.html'
    require: 'ngModel'
    link: ($scope, elem, attr, ngModel) ->

      held = false
      oldMargin = 0;
      newMargin = 0;
      pageX = 0
      margin = 0
      spaceWidth = 0
      scaleSize = 0
      xDiff = 0

      scrollbar = elem.find('.scrollbar')

      # respond to change in scrollbar width
      $scope.$watch ->
        scrollbar.css 'width'
      , (newVal, oldVal) ->
        if newVal != oldVal
          spaceWidth = getSpaceWidth()
          newMargin = spaceWidth * ngModel.$viewValue
          updateMargin newMargin

          if spaceWidth is 0
            ngModel.$setViewValue 1

      $scope.$watch ->
        ngModel.$viewValue
      , (val, oldVal) ->
        if val isnt oldVal
          val = parseFloat(val) || 0
          val = if (val > 1) then 1 else val
          val = if (val < 0) then 0 else val

          if !held and angular.isNumber val

            ngModel.$setViewValue val

            newMargin = spaceWidth*val
            updateMargin newMargin


      # avoid text selection when dragging the scrollbar
      disableSelect = ->
        $window.$(document.body).css
          'userSelect': 'none'

      enableSelect = ->
        $window.$(document.body).css
          'userSelect': ''

      getMarginLeft = ->
        parseFloat scrollbar.css('marginLeft').replace /px/, ''

      getElemWidth = ->
        parseFloat elem.css('width').replace /px/, ''

      getScrollBarWidth = ->
        parseFloat scrollbar.css('width').replace /px/, ''

      getSpaceWidth = ->
        getElemWidth() - getScrollBarWidth()

      updateMargin = (newMargin) ->
        if newMargin > spaceWidth then newMargin = spaceWidth
        if newMargin < 0 then newMargin = 0
        scrollbar.css marginLeft: "#{newMargin}px"

      elem.on 'mousedown', (e) ->
        held = true
        pageX = e.pageX
        disableSelect()

        oldMargin = getMarginLeft()
        spaceWidth = getSpaceWidth()

      $window.$(document).on 'mouseup', (e) ->
        held = false
        enableSelect()

      $window.$(document).on 'mousemove', (e) ->
        if held
          xDiff = e.pageX - pageX
          newMargin = oldMargin + xDiff

          updateMargin newMargin

          # avoid dividing with spaceWidth = 0, else result is NaN
          val = if spaceWidth > 0 then Math.round((newMargin)/spaceWidth*1000)/1000 else 0
          ngModel.$setViewValue val

]