window.ChaiBioTech.ngApp.directive 'chartDragScroll', [
  '$window'
  ($window) ->
    restrict: 'EA'
    require: 'ngModel'
    link: ($scope, elem, attrs, ngModel) ->

      held = false
      mouseIn = false
      pageX = 0
      oldVal = 0
      elemWith = elem.css('width').replace /px/, ''

      # avoid text selection when dragging the scrollbar
      disableSelect = ->
        $window.$(document.body).css
          'userSelect': 'none'

      enableSelect = ->
        $window.$(document.body).css
          'userSelect': ''

      getWidthAttr = ->
        parseInt(elem.attr 'width')

      elem.on 'mousedown', (e) ->
        held = true
        oldVal = ngModel.$viewValue
        pageX = e.pageX
        disableSelect()

      elem.on 'mousemove', (e) ->
        if held and mouseIn and (oldVal isnt 'FULL')
          xDiff = e.pageX - pageX
          widthattr = getWidthAttr()-elemWith
          valPercent = (xDiff)/widthattr
          val = oldVal+(-valPercent)
          ngModel.$setViewValue val


      $($window).on 'mouseup', (e) ->
        held = false
        enableSelect()

      elem.on 'mouseenter', (e) ->
        mouseIn = true

      elem.on 'mouseout', (e) ->
        mouseIn = false
]